# Framework Foundry

Trên thực tế, Remix IDE có nhiều giới hạn về mặt tính năng nên chúng ta sẽ sử dụng Foundry, một framework để phát triển, test và deploy smart contract.

## Cài đặt
Truy cập website [GetFoundry.sh](https://getfoundry.sh/) và đi theo hướng dẫn.

## Bắt đầu
Truy cập và làm theo hướng dẫn trong [Foundry Book](https://book.getfoundry.sh/getting-started/first-steps) để khởi tạo project.

## Fund Me project
Chúng ta sẽ sử dụng project [Foundry FundMe của Patrick Collins](https://github.com/Cyfrin/foundry-fund-me-f23) làm bài hướng dẫn cho phần này.

## Nội dung
1. Đầu tiên chạy `forge init foundry-fund-me`
2. Sau đó chúng ta sẽ tạo file `FundMe.sol`

`FundMe.sol`
```solidity
// FundMe.sol
// SPDX-License-Identifier: MIT
// 1. Pragma
pragma solidity ^0.8.19;
// 2. Imports
// Chúng ta import thư viện chainlink để chúng ta có thể tương tác với oracle
import {AggregatorV3Interface} from "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";
// Chúng ta import thư viện PriceConverter để chúng ta tính toán giá trị Ether
import {PriceConverter} from "./PriceConverter.sol";

// 3. Interfaces, Libraries, Contracts
// Khai báo error không phải là Owner của contract
error FundMe__NotOwner();

/**
 * @title A sample Funding Contract
 * @author Patrick Collins
 * @notice This contract is for creating a sample funding contract
 * @dev This implements price feeds as our library
 */
contract FundMe {
    // Type Declarations
    // Dòng tiếp theo có nghĩa là 
    // "sử dụng library PriceConverter cho những biến có type là uint256"
    using PriceConverter for uint256;

    // State variables
    // Khai báo 1 public constant MINIMUM_USD với giá trị $5 nhưng bằng wei nên phải nhân 10^18
    uint256 public constant MINIMUM_USD = 5 * 10 ** 18;
    // Khai báo 1 address private và immutable với tên là i_owner, i nghĩa là immutable.
    address private immutable i_owner;
    // Khai báo 1 array private chứa danh sách những người fund ether vào với tên là s_funders, s nghĩa là storage.
    address[] private s_funders;
    // Khai báo 1 mapping giữa address với uint256 private liên kết địa chỉ với số tiền fund.
    mapping(address => uint256) private s_addressToAmountFunded;
    // Khai báo contract AggregatorV3Interface private và gán vào biến s_pricefeed, s nghĩa là storage
    AggregatorV3Interface private s_priceFeed;

    // Events (we have none!)

    // Modifiers
    // Khai báo 1 modifier onlyOwner để gán vào function mà chỉ owner có thể gọi được
    modifier onlyOwner() {
        // require(msg.sender == i_owner);
        if (msg.sender != i_owner) revert FundMe__NotOwner();
        _;
    }

    // Functions Order:
    //// constructor
    //// receive
    //// fallback
    //// external
    //// public
    //// internal
    //// private
    //// view / pure

    // Khai báo constructor với 1 địa chỉ cho priceFeed ám chỉ rằng đây là địa chỉ của contract Oracle với AggregatorV3Interface
    constructor(address priceFeed) {
        // input địa chỉ vào interface và gán vào biến s_priceFeed
        s_priceFeed = AggregatorV3Interface(priceFeed);
        // Gán biến i_owner là msg.sender (người deploy contract này)
        i_owner = msg.sender;
    }

    /// @notice Funds our contract based on the ETH/USD price
    // Gửi tiền vào contract của chúng ta dựa trên giá ETH/USD
    function fund() public payable {
        // Yêu cầu msg.value phải lớn hơn MINIMUM_USD, msg.value tính bằng wei sẽ được convert ra USD cần thiết
        require(msg.value.getConversionRate(s_priceFeed) >= MINIMUM_USD, "You need to spend more ETH!");
        // require(PriceConverter.getConversionRate(msg.value) >= MINIMUM_USD, "You need to spend more ETH!");
        // Sau đó map địa chỉ của người gửi với msg.value trong mapping s_addressToAmountFunded
        s_addressToAmountFunded[msg.sender] += msg.value;
        // Sau đó thêm địa chỉ người gửi vào danh sách các funders
        s_funders.push(msg.sender);
    }

    function withdraw() public onlyOwner {
        // dùng for loop, bắt đàu từ index 0 đến index ít hơn length của danh sách, và index cộng 1 cho mỗi vòng loop
        for (uint256 funderIndex = 0; funderIndex < s_funders.length; funderIndex++) {
            // gán giá trị address tại funderIndex trong danh sách s_funders vào address funder
            address funder = s_funders[funderIndex];
            // thay đổi giá trị của mapping s_addressToAmountFunded có address là funder thành 0, tức là funder này đã withdraw
            s_addressToAmountFunded[funder] = 0;
        }
        // tạo một danh sách s_funders mới với 1 dynamic array (nôm na là danh sách) mới với kích cỡ bằng 0
        s_funders = new address[](0);
        // Transfer vs call vs Send
        // - transfer (2300 gas, throws error if any)
        // - send (2300 gas, returns bool for success or failure)
        // - call (forward all gas or set gas, returns bool for success or failure)
        // payable(msg.sender).transfer(address(this).balance);

        // Gửi toàn bộ balance của contract này tới i_owner và không có data trong transaction và trả về boolean success hay không
        (bool success,) = i_owner.call{value: address(this).balance}("");
        // Yêu cầu bool success true nếu không thì revert toàn bộ
        require(success);
    }

    function cheaperWithdraw() public onlyOwner {
        // Copy danh sách s_funders từ storage vào memory, tức là load từ global state vào local state. Thay đổi global state tốn nhiều gas hơn local state
        address[] memory funders = s_funders;
        // mappings can't be in memory, sorry!
        for (uint256 funderIndex = 0; funderIndex < funders.length; funderIndex++) {
            address funder = funders[funderIndex];
            s_addressToAmountFunded[funder] = 0;
        }
        s_funders = new address[](0);
        // payable(msg.sender).transfer(address(this).balance);
        (bool success,) = i_owner.call{value: address(this).balance}("");
        require(success);
    }

    /** Getter Functions */

    /**
     * @notice Gets the amount that an address has funded
     *  @param fundingAddress the address of the funder
     *  @return the amount funded
     */
    function getAddressToAmountFunded(address fundingAddress) public view returns (uint256) {
        return s_addressToAmountFunded[fundingAddress];
    }

    function getVersion() public view returns (uint256) {
        return s_priceFeed.version();
    }

    function getFunder(uint256 index) public view returns (address) {
        return s_funders[index];
    }

    function getOwner() public view returns (address) {
        return i_owner;
    }

    function getPriceFeed() public view returns (AggregatorV3Interface) {
        return s_priceFeed;
    }
}
```

3. Chúng ta tiếp tục tạo file `PriceConverter.sol`

`PriceConverter.sol`
```solidity
// PriceConverter.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

// import AggregatorV3Interface từ github
import {AggregatorV3Interface} from "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";

// Khai báo 1 library tên là PriceConverter
library PriceConverter {

    // Khai báo function getPrice với input là interface contract và trả về uint256
    function getPrice(AggregatorV3Interface priceFeed) internal view returns (uint256) {
        // gọi function latestRoundData() trong priceFeed
        (, int256 answer,,,) = priceFeed.latestRoundData();
        // Trả về ETH/USD rate có 18 digit (oracle có sẵn 8 số 0 nên thêm 10 số 0)
        // ETH/USD rate in 18 digit
        return uint256(answer * 10000000000);
    }

    // 1000000000
    // call it get fiatConversionRate, since it assumes something about decimals
    // It wouldn't work for every aggregator
    // Chuyển đổi số Ether ra số lượng USD
    // function getConversionRate nhận input là ethAmount với type uint256 và interface contract, trả về uint256 
    function getConversionRate(uint256 ethAmount, AggregatorV3Interface priceFeed) internal view returns (uint256) {
        // Đầu tiên lấy giá eth bằng getPrice và gán vào biến ethPrice
        uint256 ethPrice = getPrice(priceFeed);
        // Sau đó lấy ethPrice nhân với số lượng ether và chia 18 số 0
        // Trong solidity thì chúng ta nên nhân trước khi chia vì không có float
        // phép tính này là ethPrice (18 digit) * ethAmount (18 digit) / 18 digit để nhận lại 18 digit thôi
        uint256 ethAmountInUsd = (ethPrice * ethAmount) / 1000000000000000000;
        // Trả về giá trị usd của số lượng ether
        // the actual ETH/USD conversation rate, after adjusting the extra 0s.
        return ethAmountInUsd;
    }
}
```

4. Để xử lý phần import dependencies cho Foundry
```bash
forge install chainlink-brownie-contracts/contracts/
```

```bash
forge install Cyfrin/foundry-devops
```

Thêm `remappings`, `ffi` và `[rpc_endpoints]` vào file `foundry.toml` 
```toml
[profile.default]
src = "src"
out = "out"
libs = ["lib"]
remappings = ['@chainlink/contracts/=lib/chainlink-brownie-contracts/contracts/']
ffi = true
fs_permissions = [{ access = "read", path = "./broadcast" }]

[etherscan]
mainnet = { key = "${ETHERSCAN_API_KEY}" }
sepolia = { key = "${ETHERSCAN_API_KEY}" }

[rpc_endpoints]
sepolia = "${SEPOLIA_RPC_URL}"

# See more config options https://github.com/foundry-rs/foundry/blob/master/crates/config/README.md#all-options
```

5. Thêm file `.env` và thêm vào `SEPOLIA_RPC_URL`
`SEPOLIA_RPC_URL` có thể lấy được trên Ankr, Allthatnodes, v.v...

```bash
SEPOLIA_RPC_URL=https://xxxxxx/xxxxx
```

6. Trong thư mục `tests` chúng ta tạo 3 thư mục con `unit`, `integration`, `mocks` và các file `FundMeTest.t.sol`, `interactionsTest.t.sol`, `MockDataFeedAggregator.sol`

```bash
.
└── tests
    ├── integration
    │   └── interactionsTest.t.sol
    ├── mocks
    │   └── MockDataFeedAggregator.sol
    └── unit
        └── FundMeTest.t.sol
```

Copy nội dung của 3 file vào

`FundMeTest.t.sol`
```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.19;

import { DeployFundMe } from "../../script/DeployFundMe.s.sol";
import { FundMe } from "../../src/FundMe.sol";
import { HelperConfig } from "../../script/HelperConfig.s.sol";
import { Test, console } from "forge-std/Test.sol";
import { StdCheats } from "forge-std/StdCheats.sol";

contract FundMeTest is StdCheats, Test {
    FundMe public fundMe;
    HelperConfig public helperConfig;

    uint256 public constant SEND_VALUE = 0.1 ether; // just a value to make sure we are sending enough!
    uint256 public constant STARTING_USER_BALANCE = 10 ether;
    uint256 public constant GAS_PRICE = 1;

    address public constant USER = address(1);

    // uint256 public constant SEND_VALUE = 1e18;
    // uint256 public constant SEND_VALUE = 1_000_000_000_000_000_000;
    // uint256 public constant SEND_VALUE = 1000000000000000000;

    function setUp() external {
        DeployFundMe deployer = new DeployFundMe();
        (fundMe, helperConfig) = deployer.run();
        vm.deal(USER, STARTING_USER_BALANCE);
    }

    function testPriceFeedSetCorrectly() public view {
        address retreivedPriceFeed = address(fundMe.getPriceFeed());
        (address expectedPriceFeed) = helperConfig.activeNetworkConfig();
        assertEq(retreivedPriceFeed, expectedPriceFeed);
    }

    function testFundFailsWithoutEnoughETH() public {
        vm.expectRevert();
        fundMe.fund();
    }

    function testFundUpdatesFundedDataStructure() public {
        vm.startPrank(USER);
        fundMe.fund{value: SEND_VALUE}();
        vm.stopPrank();

        uint256 amountFunded = fundMe.getAddressToAmountFunded(USER);
        assertEq(amountFunded, SEND_VALUE);
    }

    function testAddsFunderToArrayOfFunders() public {
        vm.startPrank(USER);
        fundMe.fund{value: SEND_VALUE}();
        vm.stopPrank();

        address funder = fundMe.getFunder(0);
        assertEq(funder, USER);
    }

    // https://twitter.com/PaulRBerg/status/1624763320539525121

    modifier funded() {
        vm.prank(USER);
        fundMe.fund{value: SEND_VALUE}();
        assert(address(fundMe).balance > 0);
        _;
    }

    function testOnlyOwnerCanWithdraw() public funded {
        vm.expectRevert();
        fundMe.withdraw();
    }

    function testWithdrawFromASingleFunder() public funded {
        // Arrange
        uint256 startingFundMeBalance = address(fundMe).balance;
        uint256 startingOwnerBalance = fundMe.getOwner().balance;

        // vm.txGasPrice(GAS_PRICE);
        // uint256 gasStart = gasleft();
        // // Act
        vm.startPrank(fundMe.getOwner());
        fundMe.withdraw();
        vm.stopPrank();

        // uint256 gasEnd = gasleft();
        // uint256 gasUsed = (gasStart - gasEnd) * tx.gasprice;

        // Assert
        uint256 endingFundMeBalance = address(fundMe).balance;
        uint256 endingOwnerBalance = fundMe.getOwner().balance;
        assertEq(endingFundMeBalance, 0);
        assertEq(
            startingFundMeBalance + startingOwnerBalance,
            endingOwnerBalance // + gasUsed
        );
    }

    // Can we do our withdraw function a cheaper way?
    function testWithdrawFromMultipleFunders() public funded {
        uint160 numberOfFunders = 10;
        uint160 startingFunderIndex = 2;
        for (uint160 i = startingFunderIndex; i < numberOfFunders + startingFunderIndex; i++) {
            // we get hoax from stdcheats
            // prank + deal
            hoax(address(i), STARTING_USER_BALANCE);
            fundMe.fund{value: SEND_VALUE}();
        }

        uint256 startingFundMeBalance = address(fundMe).balance;
        uint256 startingOwnerBalance = fundMe.getOwner().balance;

        vm.startPrank(fundMe.getOwner());
        fundMe.withdraw();
        vm.stopPrank();

        assert(address(fundMe).balance == 0);
        assert(startingFundMeBalance + startingOwnerBalance == fundMe.getOwner().balance);
        assert((numberOfFunders + 1) * SEND_VALUE == fundMe.getOwner().balance - startingOwnerBalance);
    }
}
```

`MockDataFeedAggregator.sol`
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/**
 * @title MockV3Aggregator
 * @notice Based on the FluxAggregator contract
 * @notice Use this contract when you need to test
 * other contract's ability to read data from an
 * aggregator contract, but how the aggregator got
 * its answer is unimportant
 */
contract MockDataFeedAggregator {
    uint256 public constant version = 4;

    uint8 public decimals;
    int256 public latestAnswer;
    uint256 public latestTimestamp;
    uint256 public latestRound;

    mapping(uint256 => int256) public getAnswer;
    mapping(uint256 => uint256) public getTimestamp;
    mapping(uint256 => uint256) private getStartedAt;

    constructor(uint8 _decimals, int256 _initialAnswer) {
        decimals = _decimals;
        updateAnswer(_initialAnswer);
    }

    function updateAnswer(int256 _answer) public {
        latestAnswer = _answer;
        latestTimestamp = block.timestamp;
        latestRound++;
        getAnswer[latestRound] = _answer;
        getTimestamp[latestRound] = block.timestamp;
        getStartedAt[latestRound] = block.timestamp;
    }

    function updateRoundData(uint80 _roundId, int256 _answer, uint256 _timestamp, uint256 _startedAt) public {
        latestRound = _roundId;
        latestAnswer = _answer;
        latestTimestamp = _timestamp;
        getAnswer[latestRound] = _answer;
        getTimestamp[latestRound] = _timestamp;
        getStartedAt[latestRound] = _startedAt;
    }

    function getRoundData(uint80 _roundId)
        external
        view
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (_roundId, getAnswer[_roundId], getStartedAt[_roundId], getTimestamp[_roundId], _roundId);
    }

    function latestRoundData()
        external
        view
        returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
    {
        return (
            uint80(latestRound),
            getAnswer[latestRound],
            getStartedAt[latestRound],
            getTimestamp[latestRound],
            uint80(latestRound)
        );
    }

    function description() external pure returns (string memory) {
        return "v0.6/test/mock/MockV3Aggregator.sol";
    }
}
```

`interactionsTest.t.sol`
```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.19;

import { DeployFundMe } from "../../script/DeployFundMe.s.sol";
import { FundFundMe, WithdrawFundMe } from "../../script/Interactions.s.sol";
import { FundMe } from "../../src/FundMe.sol";
import { HelperConfig } from "../../script/HelperConfig.s.sol";
import { Test, console } from "forge-std/Test.sol";
import { StdCheats } from "forge-std/StdCheats.sol";

contract InteractionsTest is StdCheats, Test {
    FundMe public fundMe;
    HelperConfig public helperConfig;

    uint256 public constant SEND_VALUE = 0.1 ether; // just a value to make sure we are sending enough!
    uint256 public constant STARTING_USER_BALANCE = 10 ether;
    uint256 public constant GAS_PRICE = 1;

    address public constant USER = address(1);

    // uint256 public constant SEND_VALUE = 1e18;
    // uint256 public constant SEND_VALUE = 1_000_000_000_000_000_000;
    // uint256 public constant SEND_VALUE = 1000000000000000000;

    function setUp() external {
        DeployFundMe deployer = new DeployFundMe();
        (fundMe, helperConfig) = deployer.run();
        vm.deal(USER, STARTING_USER_BALANCE);
    }

    function testUserCanFundAndOwnerWithdraw() public {
        FundFundMe fundFundMe = new FundFundMe();
        fundFundMe.fundFundMe(address(fundMe));

        WithdrawFundMe withdrawFundMe = new WithdrawFundMe();
        withdrawFundMe.withdrawFundMe(address(fundMe));

        assert(address(fundMe).balance == 0);
    }
}
```
7. Sau đó chúng ta vào thư mục `scripts` và tạo các file `DeployFundMe.s.sol`, `HelperConfig.s.sol` và `Interactions.s.sol`

```bash
.
└── script
    ├── DeployFundMe.s.sol
    ├── HelperConfig.s.sol
    └── Interactions.s.sol
```

`DeployFundMe.s.sol`
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

import { Script } from "forge-std/Script.sol";
import { HelperConfig } from "./HelperConfig.s.sol";
import { FundMe } from "../src/FundMe.sol";

contract DeployFundMe is Script {
    function run() external returns (FundMe, HelperConfig) {
        HelperConfig helperConfig = new HelperConfig(); // This comes with our mocks!
        address priceFeed = helperConfig.activeNetworkConfig();

        vm.startBroadcast();
        FundMe fundMe = new FundMe(priceFeed);
        vm.stopBroadcast();
        return (fundMe, helperConfig);
    }
}
```

`HelperConfig.s.sol`
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import { MockDataFeedAggregator } from "../test/mocks/MockDataFeedAggregator.sol";
import { Script } from "forge-std/Script.sol";

contract HelperConfig is Script {
    NetworkConfig public activeNetworkConfig;

    uint8 public constant DECIMALS = 8;
    int256 public constant INITIAL_PRICE = 2000e8;

    struct NetworkConfig {
        address priceFeed;
    }

    event HelperConfig__CreatedMockPriceFeed(address priceFeed);

    constructor() {
        if (block.chainid == 1001) {
            activeNetworkConfig = getBaobabKlayConfig();
        } else {
            activeNetworkConfig = getOrCreateAnvilBaobabConfig();
        }
    }

    function getBaobabKlayConfig() public pure returns (NetworkConfig memory baobabNetworkConfig) {
        baobabNetworkConfig = NetworkConfig({
            priceFeed: 0x33D6ee12D4ADE244100F09b280e159659fe0ACE0 // KLAY / USDT
        });
    }

    function getOrCreateAnvilBaobabConfig() public returns (NetworkConfig memory anvilNetworkConfig) {
        // Check to see if we set an active network config
        if (activeNetworkConfig.priceFeed != address(0)) {
            return activeNetworkConfig;
        }
        vm.startBroadcast();
        MockDataFeedAggregator mockPriceFeed = new MockDataFeedAggregator(
            DECIMALS,
            INITIAL_PRICE
        );
        vm.stopBroadcast();
        emit HelperConfig__CreatedMockPriceFeed(address(mockPriceFeed));

        anvilNetworkConfig = NetworkConfig({priceFeed: address(mockPriceFeed)});
    }
}
```

`Interactions.s.sol`
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

import { Script, console } from "forge-std/Script.sol";
import { FundMe } from "../src/FundMe.sol";
import { DevOpsTools } from "foundry-devops/src/DevOpsTools.sol";

contract FundFundMe is Script {
    uint256 SEND_VALUE = 0.1 ether;

    function fundFundMe(address mostRecentlyDeployed) public {
        vm.startBroadcast();
        FundMe(payable(mostRecentlyDeployed)).fund{value: SEND_VALUE}();
        vm.stopBroadcast();
        console.log("Funded FundMe with %s", SEND_VALUE);
    }

    function run() external {
        address mostRecentlyDeployed = DevOpsTools.get_most_recent_deployment("FundMe", block.chainid);
        fundFundMe(mostRecentlyDeployed);
    }
}

contract WithdrawFundMe is Script {
    function withdrawFundMe(address mostRecentlyDeployed) public {
        vm.startBroadcast();
        FundMe(payable(mostRecentlyDeployed)).withdraw();
        vm.stopBroadcast();
        console.log("Withdraw FundMe balance!");
    }

    function run() external {
        address mostRecentlyDeployed = DevOpsTools.get_most_recent_deployment("FundMe", block.chainid);
        withdrawFundMe(mostRecentlyDeployed);
    }
}
```


8. Deploy
Sử dụng lệnh này để deploy lên testnet Sepolia

```bash
forge script script/DeployFundMe.s.sol --rpc-url $SEPOLIA_RPC_URL --account $WALLET_NAME --sender $SENDER_ADDRESS --broadcast --verify --etherscan-api-key $ETHERSCAN_API_KEY -vvvv
```

- `--sender $SENDER_ADDRESS` - thay `$SENDER_ADDRESS` bằng address của bạn
- `--account $WALLET_NAME` - bạn có thể set up bằng command `cast wallet new` và `cast wallet import`. Thay `$WALLET_NAME` bằng tên của keystore bạn lưu
- `--etherscan-api-key $ETHERSCAN_API_KEY` - thay `$ETHERSCAN_API_KEY` bằng Etherscan API key của bạn.

Sau khi chạy command trong terminal, bạn sẽ nhận 1 output tương tự như sau:

```bash 
[⠒] Compiling...
[⠆] Compiling 1 files with 0.8.19
[⠰] Solc 0.8.19 finished in 63.91ms
Compiler run successful!
Traces:
  [1044960] DeployFundMe::run()
    ├─ [489279] → new HelperConfig@0xC7f2Cf4845C6db0e1a1e91ED41Bcd0FcC1b0E141
    │   └─ ← 2222 bytes of code
    ├─ [381] HelperConfig::activeNetworkConfig() [staticcall]
    │   └─ ← 0x694AA1769357215DE4FAC081bf1f309aDC325306
    ├─ [0] VM::startBroadcast()
    │   └─ ← ()
    ├─ [486134] → new FundMe@0x85bb6d27571C3175c81fe212c0decCA2202147b9
    │   └─ ← 2316 bytes of code
    ├─ [0] VM::stopBroadcast()
    │   └─ ← ()
    └─ ← FundMe: [0x85bb6d27571C3175c81fe212c0decCA2202147b9], HelperConfig: [0xC7f2Cf4845C6db0e1a1e91ED41Bcd0FcC1b0E141]


Script ran successfully.

== Return ==
0: contract FundMe 0x85bb6d27571C3175c81fe212c0decCA2202147b9
1: contract HelperConfig 0xC7f2Cf4845C6db0e1a1e91ED41Bcd0FcC1b0E141

## Setting up 1 EVM.
==========================
Simulated On-chain Traces:

  [486134] → new FundMe@0x85bb6d27571C3175c81fe212c0decCA2202147b9
    └─ ← 2316 bytes of code


==========================

Chain 11155111

Estimated gas price: 3.978800998 gwei

Estimated total gas used for script: 749192

Estimated amount required: 0.002980885877293616 ETH

==========================

###
Finding wallets for all the necessary addresses...
Enter keystore password:
##
Sending transactions [0 - 0].
⠁ [00:00:00] [#####################################################################################################] 1/1 txes (0.0s)
Transactions saved to: /****************/broadcast/DeployFundMe.s.sol/11155111/run-latest.json

Sensitive values saved to: /****************/cache/DeployFundMe.s.sol/11155111/run-latest.json

##
Waiting for receipts.
⠉ [00:00:19] [#################################################################################################] 1/1 receipts (0.0s)
##### sepolia
✅  [Success]Hash: 0x8d0e5f4e3c930482cfc35c35eebdd0c0e816a93b8bdd1a9f88d4e0bef29ad487
Contract Address: 0x85bb6d27571C3175c81fe212c0decCA2202147b9
Block: 5577897
Paid: 0.00200707724261145 ETH (576462 gas * 3.481716475 gwei)


Transactions saved to: /****************/broadcast/DeployFundMe.s.sol/11155111/run-latest.json

Sensitive values saved to: /****************/cache/DeployFundMe.s.sol/11155111/run-latest.json



==========================

ONCHAIN EXECUTION COMPLETE & SUCCESSFUL.
Total Paid: 0.00200707724261145 ETH (576462 gas * avg 3.481716475 gwei)
##
Start verification for (1) contracts
Start verifying contract `0x85bb6d27571C3175c81fe212c0decCA2202147b9` deployed on sepolia

Submitting verification for [src/FundMe.sol:FundMe] 0x85bb6d27571C3175c81fe212c0decCA2202147b9.

Submitted contract for verification:
        Response: `OK`
        GUID: `zsqk661nw5n7ig3mjfucknzb1itwfqjzguswncz5c3rpgisc3u`
        URL: https://sepolia.etherscan.io/address/0x85bb6d27571c3175c81fe212c0decca2202147b9
Contract verification status:
Response: `NOTOK`
Details: `Pending in queue`
Contract verification status:
Response: `OK`
Details: `Pass - Verified`
Contract successfully verified
All (1) contracts were verified!

Transactions saved to: /****************/broadcast/DeployFundMe.s.sol/11155111/run-latest.json

Sensitive values saved to: /****************/cache/DeployFundMe.s.sol/11155111/run-latest.json
```